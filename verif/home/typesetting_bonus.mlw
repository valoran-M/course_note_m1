use int.Int
use array.Array

type chr

val constant sp : chr (* "space" character *)
val constant nl : chr (* "newline" character *)

(* We can test whether two characters are the same by writing "c = d".
   We can test whether they are different by writing "c <> d". *)
val (=) (c d: chr) : bool ensures { result <-> c = d }

(* The 'split' function takes a text 't' (an array of characters)
   and a maximum line length 'lim' (a non-negative integer).
   This function must split the text into lines by replacing
   some spaces with newlines, so that no line containing spaces
   is longer than 'lim' characters. Only the space characters
   can be modified. If 'lim' is 0, then every space must be
   replaced with a newline.

   Write the contract and the implementation of the 'split' function
   and prove its correctness. Your specification must be complete,
   i.e., it must reflect all the conditions listed above.

   BONUS POINTS are given if your implementation makes lines as long
   as allowed by the rules above, and the contract reflects it.
*)

let split (t: array chr) (lim: int) : unit
  requires { nl  <> sp }
  requires { lim >= 0 }
  ensures { forall i: int. 0 <= i < length t /\ t[i] = sp -> 
              exists j: int. 0 <= j < i /\
                (t[j] = nl \/ j = 0) /\ i - j < lim }
  ensures  { forall i. 0 <= i < length t /\ old t[i] <> sp ->
               old t[i] = t[i] }
=
  let ref space_found = false in
  let ref space_index = 0     in
  let ref line_length = 0     in
  let ghost column = Array.make (length t) 0 in
  for i=0 to Array.length t - 1 do
      invariant { space_found -> t[space_index] = sp }
      invariant { line_length <= i /\ space_index <= i }
      invariant { i - line_length = 0 \/ t[i - line_length] = nl }
      invariant { forall x. 0 <= x < i /\ t[x] = sp ->
                    (x - column[x] = 0 \/ t[x - column[x]] = nl) /\ 
                     x - column[x] < lim }
      invariant { length t <> 0 -> 0 <= space_index < length t }
      invariant { forall n. 0 <= n < length t /\ old t[n] <> sp ->
                    old t[n] = t[n] }
    if t[i] = sp then (
      space_found <- true;
      space_index <- i
    );
    line_length <- line_length + 1;
    column[i] <- line_length;
    if line_length > lim && space_found then (
      t[space_index] <- nl;
      space_found <- false;
      line_length <- line_length - (i - space_index)
    )
  done
